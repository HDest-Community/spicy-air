class SpicyAirHandler : HDCoreStaticEventHandler {

    int BreathIndex[MAXPLAYERS];
    int BreathHoldTimer[MAXPLAYERS];
    double AirSpiciness[MAXPLAYERS];
    int RealSpiciness[MAXPLAYERS];
    int SpicyLevel[MAXPLAYERS];

    // List of map names to completely ignore.
    Array<name> mapBlacklist;

    // List of persistent classes to completely ignore.
    Array<name> thingBlacklist;

    // List of ClassNames to consider to add spicy
    Array<name> spicySources;

    // List of ClassNames to consider to remove spicy
    Array<name> spicySinks;

    private transient Service _HHFunc;
    private transient Service _RespService;

    private transient CVar _enabled;

    private transient CVar _losFactor;

    private transient CVar _actorSectorRatio;
    private transient CVar _distCalc;

    private transient CVar _damageType;
    private transient CVar _maxSpiciness;

    private transient CVar _underwaterClean;

    private transient CVar _bossesSpicySources;
    private transient CVar _friendliesSpicySources;
    private transient CVar _monstersSpicySources;
    private transient CVar _shieldsSpicySources;

    private transient CVar _spicySinkDistance;
    private transient CVar _spicySourceDistance;

    private transient CVar _damageThreshold;
    private transient CVar _radsuitProtection;
    private transient CVar _hhelmetProtection;

    override void beforeProcessCommands() {
        if (!_HHFunc) _HHFunc                                 = ServiceIterator.Find("HHFunc").Next();
        if (!_RespService) _RespService                       = ServiceIterator.Find("UaS_RespiratorStatus").Next();

        if (!_enabled) _enabled                               = CVar.GetCVar('spicy_enabled');

        if (!_losFactor) _losFactor                           = CVar.GetCVar('spicy_los_factor');

        if (!_actorSectorRatio) _actorSectorRatio             = CVar.GetCVar('spicy_actor_sector_ratio');
        if (!_distCalc) _distCalc                             = CVar.GetCVar('spicy_dist_calc');

        if (!_damageType) _damageType                         = CVar.GetCVar('spicy_damage_type');
        if (!_maxSpiciness) _maxSpiciness                     = CVar.GetCVar('spicy_max_spiciness');

        if (!_damageThreshold) _damageThreshold               = CVar.GetCVar('spicy_damagethreshold');
        if (!_radsuitProtection) _radsuitProtection           = CVar.GetCVar('spicy_radsuitprotection');
        if (!_hhelmetProtection) _hhelmetProtection           = CVar.GetCVar('spicy_hhelmetprotection');
        
        if (!_underwaterClean) _underwaterClean               = CVar.GetCVar('spicy_underwater_clean');

        if (!_bossesSpicySources) _bossesSpicySources         = CVar.GetCVar('spicy_sources_bosses');
        if (!_friendliesSpicySources) _friendliesSpicySources = CVar.GetCVar('spicy_sources_friendlies');
        if (!_monstersSpicySources) _monstersSpicySources     = CVar.GetCVar('spicy_sources_monsters');
        if (!_shieldsSpicySources) _shieldsSpicySources       = CVar.GetCVar('spicy_sources_shields');

        if (!_spicySinkDistance) _spicySinkDistance           = CVar.GetCVar('spicy_sink_distance');
        if (!_spicySourceDistance) _spicySourceDistance       = CVar.GetCVar('spicy_source_distance');
    }

    override void processCommand(HDCoreCommand cmd) {
        
        switch (cmd.command) {
            case 'addSpicyMapBlacklist': {
                let name = cmd.getNameParam("name");

                if (name && mapBlacklist.find(name) == mapBlacklist.size()) mapBlacklist.push(name);
                break;
            }
            case 'removeSpicyMapBlacklist': {
                let name = cmd.getNameParam("name");

                if (name && mapBlacklist.find(name) < mapBlacklist.size()) mapBlacklist.push(mapBlacklist.find(name));
                break;
            }
            case 'clearSpicyMapBlacklist': {
                mapBlacklist.clear();
                break;
            }
            case 'addSpicyThingBlacklist': {
                let name = cmd.getNameParam("name");

                if (name && thingBlacklist.find(name) == thingBlacklist.size()) thingBlacklist.push(name);
                break;
            }
            case 'removeSpicyThingBlacklist': {
                let name = cmd.getNameParam("name");

                if (name && thingBlacklist.find(name) < thingBlacklist.size()) thingBlacklist.delete(thingBlacklist.find(name));
                break;
            }
            case 'clearSpicyThingBlacklist': {
                thingBlacklist.clear();
                break;
            }
            case 'addSpicySource': {
                let name = cmd.getNameParam("name");

                if (name && spicySources.find(name) == spicySources.size()) spicySources.push(name);
                break;
            }
            case 'removeSpicySource': {
                let name = cmd.getNameParam("name");

                if (name && spicySources.find(name) < spicySources.size()) spicySources.delete(spicySources.find(name));
                break;
            }
            case 'clearSpicySources': {
                spicySources.clear();
                break;
            }
            case 'addSpicySink': {
                let name = cmd.getNameParam("name");

                if (name && spicySinks.find(name) == spicySinks.size()) spicySinks.push(name);
                break;
            }
            case 'removeSpicySink': {
                let name = cmd.getNameParam("name");

                if (name && spicySinks.find(name) < spicySinks.size()) spicySinks.delete(spicySinks.find(name));
                break;
            }
            case 'clearSpicySinks': {
                spicySinks.clear();
                break;
            }
            default: 
                break;
        }
    }

    override void afterProcessCommands() {
        if (HDCore.ShouldLog('SpicyAir.SpicyAirHandler', LOGGING_DEBUG)) {

            let msg = "Configured Spicy Sinks:\n";
            
            forEach(sink : spicySinks) msg = msg.." * "..sink.."\n";
            
            HDCore.Log('SpicyAir.SpicyAirHandler', LOGGING_DEBUG, msg);
            

            msg = "Configured Spicy Sources:\n";
            
            forEach(source : spicySources) msg = msg.." * "..source.."\n";
            
            HDCore.Log('SpicyAir.SpicyAirHandler', LOGGING_DEBUG, msg);
        }
    }

    override void WorldLoaded(WorldEvent e) {

        super.WorldLoaded(e);

        if (e.IsReopen) return;

        // Initialize  Status
        for (int i = 0; i < MAXPLAYERS; i++) {
            BreathIndex[i] = 0;
            AirSpiciness[i] = 0;
            RealSpiciness[i] = 0;
            SpicyLevel[i] = 0;
            BreathHoldTimer[i] = Level.AirSupply; // * players[i].mo.AirCapacity;
        }
    }

    override void RenderUnderlay(RenderEvent e) {

        // Grab the current player and if they don't exist or are invisible, quit.
        let plr = HDPlayerPawn(e.Camera);
        if (!plr || plr.bINVISIBLE || plr.bSHADOW) return;

        // Check if the player has either a Gas Mask or Helmet equipped.
        let mask = Despicyto(plr.FindInventory('Despicyto'));
        bool hasHelmet = _HHFunc && _HHFunc.GetIntUI("GetShowHUD", objectArg: plr);

        if (
            (
                (mask && mask.Worn)
                || (hasHelmet && _hhelmetProtection.GetInt() > 0)
            )
            && !automapactive
        ) {
            int MaskHeight = int(Screen.GetHeight() * 2.2);
            int MaskWidth = int(Screen.GetWidth() * MaskHeight * 1.2) / Screen.GetHeight();
            int MaskOffX = -((MaskWidth - Screen.GetWidth()) >> 1);
            int MaskOffY = -((MaskHeight - Screen.GetHeight()) >> 1);

            let goggles = PortableLiteAmp(plr.FindInventory('PortableLiteAmp'));

            // Only handle breathing animation if Spicy Air is enabled
            if (_enabled.GetBool()) {
                double breathFactor = BreathIndex[plr.PlayerNumber()] / double(GetBreathDelay(plr));
                double breathAlpha = clamp(CVar.GetCVar('spicy_breathalpha', players[plr.PlayerNumber()]).GetFloat(), 0.0, 1.0);
                breathFactor = 0.1 + min(1.0, abs(breathFactor > 0 ? breathFactor * 2.25 : breathFactor)) * breathAlpha;

                if (goggles && goggles.worn) {
                    breathFactor *= 0.1 - (goggles.amplitude / PortableLiteAmp.NITEVIS_MAX) * 0.05;
                }

                if (breathFactor > 0.0) {
                    Screen.DrawTexture(
                        TexMan.CheckForTexture("DESPMOIS"),
                        true,
                        MaskOffX - (int(plr.wepbob.x * 0.5)),
                        MaskOffY - (int(plr.wepbob.y * 0.5)),
                        DTA_Alpha, breathFactor,
                        DTA_DestWidth, MaskWidth,
                        DTA_DestHeight, MaskHeight
                    );
                }
            }

            // Rendering the Overlay can happen even if the mod is disabled,
            // as the gas mask feels a bit pointless otherwise.
            double maskAlpha = clamp(CVar.GetCVar('spicy_gasmaskalpha', players[plr.PlayerNumber()]).GetFloat(), 0.0, 1.0);
            if ((mask && mask.Worn) && !(goggles && goggles.worn) && maskAlpha > 0.0) {
				Screen.DrawTexture(
                    TexMan.CheckForTexture("DESPMASK"),
                    true,
                    MaskOffX - (int(plr.wepbob.x * 0.5)),
                    MaskOffY - (int(plr.wepbob.y * 0.5)),
                    DTA_Alpha, maskAlpha,
                    DTA_DestWidth, MaskWidth,
                    DTA_DestHeight, MaskHeight
                );
            }
        }
    }

    override void WorldTick() {

        // If we can't find the CVAR, or it's not set to true, or the level is frozen, quit.
        if (!(_enabled && _enabled.GetBool()) || Level.IsFrozen()) return;
        
        // TODO: Refactor following logic into separate functions
        for (int i = 0; i < MAXPLAYERS; i++) {
            let plr = HDPlayerPawn(players[i].mo);

            // If the player isn't an HDPlayerPawn or is dead, skip.
            if (!plr || plr.Health <= 0) continue;

            // If the player has godmode on, clear their spiciness and skip.
            if (plr.player.cheats&(CF_GODMODE2|CF_GODMODE)) {
                RealSpiciness[i] = 0;
                continue;
            }

            // Handle Player Breathing
            // -----------------------
            // The player is "currently breathing" if:
            // - They are not submerged underwater, or if they are, they're wearing a UaS Respirator and it has air in it
            // - They are wearing a gas mask
            // - They are wearing a Hideous Helmet and it provides protection against spicy air
            let mask = Despicyto(plr.FindInventory('Despicyto'));
            bool hasHelmet = _HHFunc && _HHFunc.GetInt("GetShowHUD", objectArg: plr);
            bool wearingRespirator = _RespService && _RespService.GetInt("IsWorn", objectArg: plr);
            bool respiratorAir = _RespService && _RespService.GetInt("AirRemaining", objectArg: plr);
            int bDelay = GetBreathDelay(plr);
            if (
                (plr.WaterLevel < 3 || (wearingRespirator && respiratorAir > 0))
                || (mask && mask.Worn)
                || (hasHelmet && _hhelmetProtection.GetInt() > 0)
            ) {
                BreathIndex[i]++;

                if (BreathIndex[i] >= bDelay) BreathIndex[i] = -bDelay;
            } else {
                BreathIndex[i] = 0;
            }

            // Display Visual/Auditory Feedback of breathing
            if ((mask && mask.Worn) || (hasHelmet && _hhelmetProtection.GetInt() > 0)) {
                int bSpeed = GetBreathSpeed(plr);

                double vol = clamp(CVar.GetCVar('spicy_breathingvolume', players[i]).GetFloat(), 0, 1);
                
                if (BreathIndex[i] == 0 && vol > 0) {
                    plr.A_StartSound("SpicyAir/Mask/BreatheOut"..bSpeed, 35, CHANF_LOCAL, vol, pitch: frandom[breathrand](0.97, 1.0));
                }

                if (BreathIndex[i] == -bDelay) {

                    if (vol > 0) {
                        plr.A_StartSound("SpicyAir/Mask/BreatheIn"..bSpeed, 36, CHANF_LOCAL, vol, pitch: frandom[breathrand](0.97, 1.0));
                    }

                    if (mask && mask.Worn) {
                        if (mask.weaponStatus[DEProp_FilterLife1] > 0) {
                            mask.weaponStatus[DEProp_FilterLife1]--;
                        } else if (mask.weaponStatus[DEProp_FilterLife2] > 0) {
                            mask.weaponStatus[DEProp_FilterLife2]--;
                        }
                    }
                }
            }

            // Handle Air Toxicity Logic
            // -------------------------
            // - If the air being breathed in is clean, adjust "breath hold timer" depending if the player is underwater or wearing a respirator providing air
            // - If the air being breathed in is clean or filtered, reduce spiciness while the player is breathing out
            // - If the air being breathed in is not clean nor filtered, if the player has breath left to hold, reduce timer; otherwise add spiciness while the player is breathing in
            // - If the player has gathered enough spiciness in their system, increase their spicy level
            // - As long as the player still has some amount of spiciness levels in their system, every time the timer ticks, add aggro based on their spicy level
            let aggroTime = 35 * GetAggroThreshold(plr);

            let maxSpiciness = _maxSpiciness.GetInt();
            let rawSpiciness = GetAirSpiciness(plr);
            let spiciness = clamp(round(rawSpiciness), -maxSpiciness, maxSpiciness);

            // Store the raw spiciness for service to provide.
            AirSpiciness[i] = rawSpiciness;

            if (IsAirClean(plr) || AirSpiciness[i] <= 0.0) {

                if (!(Level.time % TICRATE)) HDCore.log('SpicyAir.SpicyAirHandler', LOGGING_DEBUG, "[Player #"..(i + 1).."] Air is clean! "..(BreathHoldTimer[i] / 35).."s"..", "..RealSpiciness[i]);

                if (BreathIndex[i] > 0) RealSpiciness[i] -= max(RealSpiciness[i] / aggroTime, abs(spiciness));
                else if (BreathIndex[i] < 0) BreathHoldTimer[i] = clamp(BreathHoldTimer[i] + ((plr.WaterLevel < 3 || (wearingRespirator && respiratorAir > 0)) ? 5 : -1), 0, Level.AirSupply * plr.AirCapacity);
            } else if (IsAirFiltered(plr)) {

                if (!(Level.time % TICRATE)) HDCore.log('SpicyAir.SpicyAirHandler', LOGGING_DEBUG, "[Player #"..(i + 1).."] Air is filtered! "..(BreathHoldTimer[i] / 35).."s"..", "..RealSpiciness[i]);
                
                if (BreathIndex[i] > 0) RealSpiciness[i] -= min(RealSpiciness[i] / aggroTime, abs(spiciness));
                BreathHoldTimer[i] = clamp(BreathHoldTimer[i] + 3, 0, Level.AirSupply * plr.AirCapacity);
            } else if (BreathHoldTimer[i] > 0) {

                if (!(Level.time % TICRATE)) HDCore.log('SpicyAir.SpicyAirHandler', LOGGING_DEBUG, "[Player #"..(i + 1).."] Air is spicy! "..(BreathHoldTimer[i] / 35).."s"..", "..RealSpiciness[i]);

                BreathHoldTimer[i] = clamp(BreathHoldTimer[i] - 1, 0, Level.AirSupply * plr.AirCapacity);
            } else {

                if (!(Level.time % TICRATE)) HDCore.log('SpicyAir.SpicyAirHandler', LOGGING_DEBUG, "[Player #"..(i + 1).."] Air is spicy, too spicy! Breathing "..(BreathIndex[i] < 0 ? "in" : "out")..", "..RealSpiciness[i]);

                // If we're breathing in while it's spicier than the player,
                // or we're breathing out while it's not spicier than the player,
                // adjust the player's raw spiciness level.
                if (
                    (BreathIndex[i] < 0 && AirSpiciness[i] > RealSpiciness[i])
                    || (BreathIndex[i] > 0 && AirSpiciness[i] < RealSpiciness[i])
                ) {
                    // Force increase when breathing in, decrease when breathing out
                    // RealSpiciness[i] += spiciness;
                    RealSpiciness[i] += (BreathIndex[i] < 0 ? frandom(1 - (0.1 * maxSpiciness), 1 + (0.1 * maxSpiciness)) : -frandom(0.0001 * (11 - maxSpiciness), 0.001 * (11 - maxSpiciness))) * abs(AirSpiciness[i] - RealSpiciness[i]);
                }

                // if (BreathIndex[i] < 0 && RealSpiciness[i] <= 0.0 && BreathHoldTimer[i] < (Level.AirSupply * plr.AirCapacity)) BreathHoldTimer[i]++;
            }

            RealSpiciness[i] = clamp(RealSpiciness[i], 0, aggroTime * maxSpiciness);
            SpicyLevel[i] = clamp(RealSpiciness[i] / aggroTime, 0, maxSpiciness);

            if (!(Level.time % TICRATE)) HDCore.log('SpicyAir.SpicyAirHandler', LOGGING_DEBUG, "[Player #"..(i + 1).."] Spiciness Level: "..(RealSpiciness[i] / aggroTime));

            if (SpicyLevel[i] > 0 && !(Level.time % aggroTime)) {
                HDCore.log('SpicyAir.SpicyAirHandler', LOGGING_DEBUG, "[Player #"..(i + 1).."] Oof ouch, my blood! x"..SpicyLevel[i]);

                DealSpicyDamage(plr, SpicyLevel[i]);
            }
        }
    }

    override void WorldThingSpawned(WorldEvent e) {

        // If we can't find the CVAR or it's not set to true, quit.
        if (!_enabled && _enabled.GetBool()) return;

        // If the thing doesn't exist, quit.
        if (!e.thing) return;

        let mo = e.thing;

        // If the thing is blacklisted, quit.
        forEach (bl : thingBlacklist) if (mo is bl) return;

        // If the current map is blacklisted, quit.
        forEach (bl : mapBlacklist) if (Level.mapName == bl) return;
        
        // Mark if the thing is sink.
        let isSink = false;
        forEach (sink : spicySinks) {
            if (mo is sink) {
                isSink = true;
                break;
            }
        }
        
        // Mark if the thing is source.
        let isSource = false;
        forEach (source : spicySources) {
            if (mo is source) {
                isSource = true;
                break;
            }
        }

        // Grab the magnitude of the source/sink based on whether we're counting shields or just base HP.
        // TODO: Move this into tracker tokens
        let amt = (_shieldsSpicySources.Getbool() && mo.CountInv('HDMagicShield'))
                    ? mo.CountInv('HDMagicShield')
                    : mo.SpawnHealth();

        // If there's no amount to consider or thing is neither sink nor source, quit.
        if (amt <= 0 || !(isSink || isSource)) return;

        // If the thing has no trackers and is a spicy sink, give it a token.
        if (!(mo.countInv('SpicyAirSourceToken') || mo.countInv('SpicyAirSinkToken')) && isSink) {
            HDCore.log('SpicyAir.SpicyAirHandler', LOGGING_DEBUG, "Giving "..amt.."x Sink Token to "..mo.getClassName().." @"..mo.pos);

            mo.A_GiveInventory('SpicyAirSinkToken', max(amt, 1));
        }

        // If the thing has no trackers and is a spicy source, give it a token.
        if (!(mo.countInv('SpicyAirSourceToken') || mo.countInv('SpicyAirSinkToken')) && isSource) {
            HDCore.log('SpicyAir.SpicyAirHandler', LOGGING_DEBUG, "Giving "..amt.."x Source Token to "..mo.getClassName().." @"..mo.pos);

            mo.A_GiveInventory('SpicyAirSourceToken', max(amt, 1));
        }
    }

    // The air is considered clean if:
    //  - The current map is blacklisted
    //  - The player is currently inside of a sector marked as non-spicy
    //  - The player is currently in deep water and doing so is considered clean
    bool IsAirClean(HDPlayerPawn plr) {

        bool cleanLevel = false;
        forEach (bl : mapBlacklist) {
            if (Level.MapName == bl) {
                cleanLevel = true;
                break;
            }
        }

        if (!(Level.time % TICRATE) && cleanLevel) HDCore.log('SpicyAir.SpicyAirHandler', LOGGING_DEBUG, "Current Level is Clean: "..Level.mapName);


        bool cleanSector = plr.CurSector.GetUDMFInt('user_nonspicysector') == 1;

        if (!(Level.time % TICRATE) && cleanSector) HDCore.log('SpicyAir.SpicyAirHandler', LOGGING_DEBUG, "Current Sector is Clean: Special");


        bool cleanUnderwater = _underwaterClean.GetBool() && plr.WaterLevel >= 3;

        if (!(Level.time % TICRATE) && cleanUnderwater) HDCore.log('SpicyAir.SpicyAirHandler', LOGGING_DEBUG, "Current Sector is Clean: Underwater");

        return cleanLevel|cleanSector|cleanUnderwater;
    }

    bool IsAirFiltered(HDPlayerPawn plr) {
        let mask = Despicyto(plr.FindInventory('Despicyto'));

        return mask && mask.Worn && mask.A_GetTotalAir(false) > 0;
    }

    clearscope int GetBreathDelay(HDPlayerPawn plr) {
        return 90 - int(min(max(plr.fatigue * 2, plr.bloodpressure / 1.3, 110 * (1.0 - plr.beatmax / 35.0)), 80));
    }

    clearscope int GetBreathSpeed(HDPlayerPawn plr) {
        return 3 - int(min(GetBreathDelay(plr) / 35, 2));
    }

    double GetAirSpiciness(HDPlayerPawn plr) {

        double activeSinks = 0.0;
        double activeSources = 0.0;

        int losFactor = _losFactor.GetInt();

        let iter = ThinkerIterator.create('Actor');

        Actor mo;
        while (mo = Actor(iter.next())) {

            let blacklisted = false;
            forEach (bl : thingBlacklist) {
                if (mo is bl) {
                    blacklisted = true;
                    break;
                }
            }
            
            // If the thing is blacklisted, skip.
            if (blacklisted) continue;

            // If thing is dead, skip.
            if (mo.health <= 0) continue;

            // If thing is an item and is owned, skip.
            if (mo is 'Inventory' && Inventory(mo).owner && mo.pos == (0, 0, 0)) continue;

            // If thing is a monster when we're not looking for it, skip.
            if (!(_monstersSpicySources.GetBool()) && !mo.bISMONSTER) continue;

            // If thing is friendly when we're not looking for it, skip.
            if (!(_friendliesSpicySources.GetBool()) && mo.bFRIENDLY) continue;

            // If thing is a boss when we're not looking for one, skip.
            if (!(_bossesSpicySources.GetBool()) && mo.bBOSS) continue;

            let sourceAmt = mo.countInv('SpicyAirSourceToken');
            let sinkAmt = mo.countInv('SpicyAirSinkToken');

            // If thing does not contain a tracker, skip.
            if (!(sourceAmt || sinkAmt)) continue;

            let canSeePlayer = mo.CheckSight(plr);
            let dist = max(1, plr.Distance3D(mo));

            if (canSeePlayer || losFactor > 0) {
                if (sinkAmt) {
                    HDCore.log('SpicyAir.SpicyAirHandler', LOGGING_TRACE, "Adding Spicy Air Sink: "..mo.getClassName().." @ "..mo.pos);

                    activeSinks += calcDist(
                        sinkAmt
                            * ((mo.scale.x + mo.scale.y) / 2.0)
                            * (canSeePlayer ? 1.0 : (losFactor / 100.0)),
                        _spicySinkDistance.GetFloat() * HDCONST_ONEMETRE,
                        dist
                    );
                } else if (sourceAmt) {
                    HDCore.log('SpicyAir.SpicyAirHandler', LOGGING_TRACE, "Adding Spicy Air Source: "..mo.getClassName().." @ "..mo.pos);
                    
                    activeSources += calcDist(
                        sourceAmt
                            * ((mo.scale.x + mo.scale.y) / 2.0)
                            * (canSeePlayer ? 1.0 : (losFactor / 100.0)),
                        _spicySourceDistance.GetFloat() * HDCONST_ONEMETRE,
                        dist
                    );
                }
            }
        }

        if (!(Level.time % TICRATE)) HDCore.log(
            'SpicyAir.SpicyAirHandler', LOGGING_DEBUG, "Current Spicy Air Status:\n"
            .." * Spicy Sources: "..activeSources.."\n"
            .." * Spicy Sinks: "..activeSinks.."\n"
            .." * Air Spiciness: "..(activeSources - activeSinks)
        );

        return activeSources - activeSinks;
    }

    int GetAggroThreshold(HDPlayerPawn plr) {
        let totalBonus = 0.0;
        
        if (plr.FindInventory('WornRadsuit')) {
            totalBonus += _radsuitProtection.GetInt();
        }
        
        if (_HHFunc && _HHFunc.GetInt("GetShowHUD", objectArg: plr)) {
            totalBonus += _hhelmetProtection.GetInt();
        }
        
        let threshold = _damageThreshold.GetInt();
        return threshold + (threshold * totalBonus);
    }

    private double calcDist(double amount, double factor, double dist) {
        HDCore.log('SpicyAir.SpicyAirHandler', LOGGING_TRACE, "Calculating distance: ("..amount..", "..factor..", "..dist..")...");

        let af = amount * factor;
        let denom = max(1.0, min(af, dist));

        switch (_distCalc.GetInt()) {
            case SADC_INVERSE_SQUARE:
                return af / (denom * denom);
            case SADC_INVERSE_SQRT:
                return (af / sqrt(denom)) - sqrt(af);
            case SADC_REVERSE_LOG:
                return (amount / factor) * max(1.0, log10(max(1.0, factor - dist)));
            case SADC_REVERSE_SQRT:
                return (amount / (factor * factor)) * sqrt(af - dist);
            case SADC_LINEAR:
            default:
                return clamp((af - dist) / max(1.0, factor), 0.0, Double.MAX);
        }
    }

    private void DealSpicyDamage(HDPlayerPawn plr, int amount) {
        plr.A_SetBlend(0xa7b55e, 0.15, 12);

        switch (_damageType.GetInt()) {
            case SADT_STUN:
                DealSpicyStun(plr, amount);
                break;
            case SADT_FATIGUE:
                DealSpicyFatigue(plr, amount);
                break;
            case SADT_TISSUE:
                DealSpicyTissueDamage(plr, amount);
                break;
            case SADT_BLOOD:
                DealSpicyBloodLoss(plr, amount);
                break;
            case SADT_BURN:
                DealSpicyBurns(plr, amount);
                break;
            case SADT_AGGRO:
                DealSpicyAggro(plr, amount);
                break;
            case SADT_WOUND:
                DealSpicyWound(plr, amount);
                break;
            case SADT_AUTO:
            default:

                if (amount >= SADT_STUN)    DealSpicyStun(plr, max(1, amount - SADT_STUN));
                if (amount >= SADT_FATIGUE) DealSpicyFatigue(plr, max(1, amount - SADT_FATIGUE));
                if (amount >= SADT_TISSUE)  DealSpicyTissueDamage(plr, max(1, amount - SADT_TISSUE));
                if (amount >= SADT_BLOOD)   DealSpicyBloodLoss(plr, max(1, amount - SADT_BLOOD));
                if (amount >= SADT_BURN)    DealSpicyBurns(plr, max(1, amount - SADT_BURN));
                if (amount >= SADT_AGGRO)   DealSpicyAggro(plr, max(1, amount - SADT_AGGRO));
                if (amount >= SADT_WOUND)   DealSpicyWound(plr, max(1, amount - SADT_WOUND));

                break;
        }
    }

    private void DealSpicyStun(HDPlayerPawn plr, int amount) {
        plr.stunned += 32 << amount;
    }

    private void DealSpicyFatigue(HDPlayerPawn plr, int amount) {
        plr.fatigue += 1 << amount;
    }

    private void DealSpicyTissueDamage(HDPlayerPawn plr, int amount) {
        plr.oldWoundCount += amount;
    }

    private void DealSpicyBloodLoss(HDPlayerPawn plr, int amount) {
        plr.bloodLoss += 8 << amount;
    }

    private void DealSpicyBurns(HDPlayerPawn plr, int amount) {
        plr.burnCount += amount;
    }

    private void DealSpicyAggro(HDPlayerPawn plr, int amount) {
        plr.aggravatedDamage += amount;
    }

    private void DealSpicyWound(HDPlayerPawn plr, int amount) {
        let numWounds = random(1, 1 << amount);
        for (let i = 0; i < numWounds; i++) {
            HDBleedingWound.inflict(plr, amount, damageType: 'internal');
        }
    }
}


enum SPICY_DISTANCE_CALCS {
    SADC_INVERSE_SQRT,
    SADC_INVERSE_SQUARE,
    SADC_REVERSE_LOG,
    SADC_REVERSE_SQRT,
    SADC_LINEAR
}

enum SPICY_DAMAGE_TYPES {
    SADT_AUTO,
    SADT_STUN,
    SADT_FATIGUE,
    SADT_TISSUE,
    SADT_BLOOD,
    SADT_BURN,
    SADT_AGGRO,
    SADT_WOUND
}
