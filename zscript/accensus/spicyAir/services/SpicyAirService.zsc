// general functions that are nice to have
class SpicyAirService : Service {

    override int GetIntUI(
        String request,
        String stringArg,
        int intArg,
        double doubleArg,
        Object objectArg
    ) {
        return HandleGetInt(request, stringArg, intArg, doubleArg, objectArg);
    }

    override int GetInt(
        String request,
        String stringArg,
        int intArg,
        double doubleArg,
        Object objectArg
    ) {
        return HandleGetInt(request, stringArg, intArg, doubleArg, objectArg);
    }

    clearscope int HandleGetInt(
        String request,
        String stringArg,
        int intArg,
        double doubleArg,
        Object objectArg
    ) {
        int retVal = 0;

        // Requires: objectArg (Actor)
        name reqName = request;

        switch (reqName) {
            case 'IsGasMaskWorn':
                retVal = IsGasMaskWorn(Despicyto(objectArg));
                break;
            case 'GetTotalAir':
                retVal = GetTotalAir(Despicyto(objectArg));
                break;
            case 'GetFilterBonus':
                retVal = GetFilterBonus(HDPlayerPawn(objectArg));
                break;
            case 'GetAggroThreshold':
                retVal = GetAggroThreshold(HDPlayerPawn(objectArg));
                break;
            case 'GetBreathHoldTimer':
                retVal = GetBreathHoldTimer(HDPlayerPawn(objectArg));
                break;
            case 'GetAirToxicity':
                retVal = GetAirToxicity(HDPlayerPawn(objectArg));
                break;
            case 'GetMaxAirToxicity':
                retVal = GetMaxAirToxicity(HDPlayerPawn(objectArg));
                break;
            case 'GetPlayerToxicity':
                retVal = GetPlayerToxicity(HDPlayerPawn(objectArg));
                break;
            case 'GetMaxPlayerToxicity':
                retVal = GetMaxPlayerToxicity(HDPlayerPawn(objectArg));
                break;
            default:
                HDCore.log('SpicyAir.SpicyAirService', LOGGING_WARN, "Invalid request "..request.."!");
                break;
        }

        return retVal;
    }

    override double GetDoubleUI(
        String request,
        String stringArg,
        int intArg,
        double doubleArg,
        Object objectArg
    ) {
        return HandleGetDouble(request, stringArg, intArg, doubleArg, objectArg);
    }

    override double GetDouble(
        String request,
        String stringArg,
        int intArg,
        double doubleArg,
        Object objectArg
    ) {
        return HandleGetDouble(request, stringArg, intArg, doubleArg, objectArg);
    }

    clearscope double HandleGetDouble(
        String request,
        String stringArg,
        int intArg,
        double doubleArg,
        Object objectArg
    ) {
        double retVal = 0.0;

        // Requires: objectArg (Actor)
        name reqName = request;

        switch (reqName) {
            case 'IsGasMaskWorn':
                retVal = IsGasMaskWorn(Despicyto(objectArg));
                break;
            case 'GetTotalAir':
                retVal = GetTotalAir(Despicyto(objectArg));
                break;
            case 'GetFilterBonus':
                retVal = GetFilterBonus(HDPlayerPawn(objectArg));
                break;
            case 'GetAggroThreshold':
                retVal = GetAggroThreshold(HDPlayerPawn(objectArg));
                break;
            case 'GetBreathHoldTimer':
                retVal = GetBreathHoldTimer(HDPlayerPawn(objectArg));
                break;
            case 'GetAirToxicity':
                retVal = GetAirToxicity(HDPlayerPawn(objectArg));
                break;
            case 'GetMaxAirToxicity':
                retVal = GetMaxAirToxicity(HDPlayerPawn(objectArg));
                break;
            case 'GetPlayerToxicity':
                retVal = GetPlayerToxicity(HDPlayerPawn(objectArg));
                break;
            case 'GetMaxPlayerToxicity':
                retVal = GetMaxPlayerToxicity(HDPlayerPawn(objectArg));
                break;
            default:
                HDCore.log('SpicyAir.SpicyAirService', LOGGING_WARN, "Invalid request "..request.."!");
                break;
        }

        return retVal;
    }

    override string GetStringUI(
        String request,
        String stringArg,
        int intArg,
        double doubleArg,
        Object objectArg
    ) {
        return HandleGetString(request, stringArg, intArg, doubleArg, objectArg);
    }

    override string GetString(
        String request,
        String stringArg,
        int intArg,
        double doubleArg,
        Object objectArg
    ) {
        return HandleGetString(request, stringArg, intArg, doubleArg, objectArg);
    }

    clearscope string HandleGetString(
        String request,
        String stringArg,
        int intArg,
        double doubleArg,
        Object objectArg
    ) {
        string retVal = "";

        // Requires: objectArg (Actor)
        if (request == "GetGasMaskSpriteIndex") {
            retVal = GetGasMaskSpriteIndex(Despicyto(objectArg));
        } else {
            Console.PrintF("SpicyAirService: Invalid request "..request.."! Please fix :[");
        }

        return retVal;
    }

    static clearscope bool IsGasMaskWorn(Despicyto mask) {
        return mask && mask.Worn;
    }

    static clearscope int GetTotalAir(Despicyto mask) {
        return mask ? mask.A_GetTotalAir(true) : 0;
    }

    static clearscope int GetBreathHoldTimer(HDPlayerPawn plr) {
        
        let handler = SpicyAirHandler(StaticEventHandler.Find("SpicyAirHandler"));

        return handler.BreathHoldTimer[plr.PlayerNumber()];
    }

    static clearscope string GetGasMaskSpriteIndex(Despicyto mask) {
        
        int f1 = mask.weaponStatus[DEProp_FilterLife1];
        int f2 = mask.weaponStatus[DEProp_FilterLife2];

        if (f1 >= 0 && f2 >= 0) {
            return "A";
        } else if (f1 >= 0 && f2 < 0) {
            return "B";
        } else if (f2 >= 0 && f1 < 0) {
            return "C";
        } else {
            return "D";
        }
    }

    static clearscope double GetAirToxicity(HDplayerPawn plr) {
        let handler = SpicyAirHandler(StaticEventHandler.Find("SpicyAirHandler"));

        return handler.AirSpiciness[plr.PlayerNumber()];
    }

    static clearscope int GetMaxAirToxicity(HDPlayerPawn plr) {
        let maxSpiciness = CVar.GetCVar('spicy_max_spiciness').GetInt();

        return Level.airSupply * maxSpiciness;
    }

    static clearscope int GetPlayerToxicity(HDPlayerPawn plr) {
        return plr.countInv('SpicyAirDrug');
    }

    static clearscope int GetMaxPlayerToxicity(HDPlayerPawn plr) {
        let maxSpiciness = CVar.GetCVar('spicy_max_spiciness').GetInt();

        return Level.airSupply * maxSpiciness;
    }

    static clearscope double GetFilterBonus(HDPlayerPawn plr) {
        let totalBonus = 0.0;
        
        if (HDCore.checkForItem(plr, 'WornRadsuit')) {
            totalBonus += CVar.GetCVar('spicy_radsuitprotection').GetInt();
        }
        
        if (HDCore.checkForItem(plr, 'HHelmetWorn')) {
            totalBonus += CVar.GetCVar('spicy_hhelmetprotection').GetInt();
        }

        return totalBonus / 100.0;
    }

    static clearscope int GetAggroThreshold(HDPlayerPawn plr) {
        let threshold = Level.airSupply;
        return threshold + (threshold * GetFilterBonus(plr));
    }
}