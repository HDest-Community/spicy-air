class SpicyAirDummy : IdleDummy abstract {

    name tokenName; property tokenName:tokenName;

    default {
        health 100;
    }

    override void postBeginPlay() {

        super.postBeginPlay();

        // If we can't find the handler, quit.
        let handler = SpicyAirHandler(StaticEventHandler.find('SpicyAirHandler'));
        if (!handler) {
            destroy();
            return;
        }

        // If we spawned in a blacklisted map, quit.
        forEach (bl : handler.mapBlacklist) {
            if (Level.mapName == bl) {
                destroy();
                return;
            }
        }

        if (!(countInv('SpicyAirSourceToken') || countInv('SpicyAirSinkToken'))) HDF.give(self, tokenName, spawnHealth());
    }
}

class SpicyAirSource : SpicyAirDummy {
    default {
        SpicyAirDummy.tokenName 'SpicyAirSourceToken';
    }
}

class SpicyAirSink : SpicyAirDummy {
    default {
        SpicyAirDummy.tokenName 'SpicyAirSinkToken';
    }
}


class SpicyAirTrackerToken : Inventory abstract {

    default {
        +Inventory.UNTOSSABLE;
    }

    override void Tick() {
        super.Tick();

        amount = GetAmount();
    }

    abstract int GetAmount();
}

class SpicyAirSinkToken : SpicyAirTrackerToken {

    override int GetAmount() {
        return owner.spawnHealth();
    }
}

class SpicyAirSourceToken : SpicyAirTrackerToken {

    protected transient CVar _enabled;
    protected transient CVar _particleMult;
    protected transient CVar _distanceMult;
    protected transient CVar _maxSpiciness;
    protected transient CVar _shieldsSpicySources;

    private transient int particleOffset;

    override void PostBeginPlay() {
        super.PostBeginPlay();

        if (!_enabled) _enabled                         = CVar.GetCVar('spicy_enabled');
        if (!_particleMult) _particleMult               = CVar.GetCVar('spicy_particle_mult');
        if (!_distanceMult) _distanceMult               = CVar.GetCVar('spicy_source_distance');
        if (!_maxSpiciness) _maxSpiciness               = CVar.GetCVar('spicy_max_spiciness');
        if (!_shieldsSpicySources) _shieldsSpicySources = CVar.GetCVar('spicy_sources_shields');

        particleOffset = random[spicyrand](0, TICRATE - 1);
    }

    override int GetAmount() {
        if (!owner) return 0;

        if (owner is 'HDMobBase' && HDMobBase(owner).maxshields > 0 && _shieldsSpicySources && _shieldsSpicySources.GetBool()) return owner.countInv('HDMagicShield');

        return owner.spawnHealth();
    }

    override void Tick() {
        super.Tick();

        // If Spicy Air is currently disabled, don't spawn VFX particles
        if (!(_enabled &&_enabled.GetBool())) { HDCore.log('SpicyAir.SpicyAirTracker', LOGGING_TRACE, "Spicy Air Not Enabled"); return; }

        let maxSpiciness = _maxSpiciness ? _maxSpiciness.GetInt() : 0;

        // If Max Spiciness is below valid amount, don't spawn VFX particles.
        if (maxSpiciness <= 0) { HDCore.log('SpicyAir.SpicyAirTracker', LOGGING_TRACE, "Max Spiciness below valid value"); return; }

        let particleMult = _particleMult ? _particleMult.GetFloat() : 0.0;

        // If Particle Multiplier is below valid amount, don't spawn VFX particles.
        if (particleMult <= 0.0) { HDCore.log('SpicyAir.SpicyAirTracker', LOGGING_TRACE, "Particle Multiplier below valid value"); return; }

        let distanceMult = _distanceMult ? _distanceMult.GetFloat() : 0.0;

        // If Distance Multiplier is below valid amount, don't spawn VFX particles.
        if (distanceMult <= 0.0) { HDCore.log('SpicyAir.SpicyAirTracker', LOGGING_TRACE, "Distance Multiplier below valid value"); return; }

        // If Level or owner are frozen, don't spawn VFX particles.
        if (Level.isFrozen() || owner.isFrozen()) { HDCore.log('SpicyAir.SpicyAirTracker', LOGGING_TRACE, "Owner/Level Frozen"); return; }

        // If no amount emitted, don't spawn VFX particles.
        if (!amount) { HDCore.log('SpicyAir.SpicyAirTracker', LOGGING_TRACE, "No amount emitted"); return; }

        // If owner is dead, don't spawn VFX particles
        if (HDMath.isDead(owner)) { HDCore.log('SpicyAir.SpicyAirTracker', LOGGING_TRACE, "Owner is Dead"); return; }

		// Grab closest playerpawn distance.
		let dist = int.MAX;
		for(int i = 0; i < MAXPLAYERS; i++) if (playerInGame[i]) dist = min(dist, owner.distance3D(players[i].mo));

        // If owner is either outside of or too far from the current player's POV
        if (dist > (HDCONST_ONEMETRE * 20) || owner.A_CheckSight("null")) { HDCore.log('SpicyAir.SpicyAirTracker', LOGGING_TRACE, "Player cannot see"); return; }

        let cycleLength = TICRATE;
        let cycleOffset = Level.time % cycleLength;
        let particleCount = (amount * maxSpiciness * particleMult) / cycleLength;

        if (
            (
                particleCount >= cycleLength
                || (
                    ((particleOffset + particleCount) < cycleLength)
                        ? (cycleOffset >= particleOffset && cycleOffset < (particleOffset + particleCount))
                        : (cycleOffset >= ((particleOffset + particleCount) % cycleLength) && cycleOffset < particleOffset)
                )
            ) && (
                particleCount > 255 || random[spicyrand]() < particleCount
            )
        ) {
            
            HDCore.log('SpicyAir.SpicyAirTracker', LOGGING_DEBUG, "particleCount="..particleCount..", cycleOffset="..cycleOffset..", cycleLength="..cycleLength..", particleOffset="..particleOffset);

            let i = cycleOffset - particleOffset;

            let dist = HDCONST_ONEMETRE * max(1, ceil(i / distanceMult)) * distanceMult;
            Vector3 pos = owner.pos + (
                frandom[spicyrand](-dist, dist),
                frandom[spicyrand](-dist, dist),
                frandom[spicyrand](-dist, dist)
            );

            // If the spawnPos is outside the level, don't spawn VFX particles.
            if (!Level.isPointInLevel(pos)) { HDCore.log('SpicyAir.SpicyAirTracker', LOGGING_TRACE, pos.." outside of level"); return; }

            let lifetime = random[spicyrand](27, 88) * 2.0;
            let vel = (frandom[spicyrand](-0.4, 0.4), frandom[spicyrand](-0.4, 0.4), frandom[spicyrand](-0.4, 0.4));
            let accel = ((frandom[spicyrand](-0.4, 0.4), frandom[spicyrand](-0.4, 0.4), frandom[spicyrand](-0.4, 0.4)) - vel) / lifetime;

            HDCore.log('SpicyAir.SpicyAirTracker', LOGGING_DEBUG, owner.getClassName().." Spawning Particle @ "..pos);

            HDF.Particle(
                owner,
                0xa7b55e,
                pos,
                lifetime: lifetime,
                size: 2.0,
                vel: vel,
                fullBright: true,
                accel: accel
            );
        }
    }
}
